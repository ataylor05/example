locals {
    secret_name_prefix = "test"
}

data "azurerm_client_config" "current" {}

data "azurerm_key_vault" "source_vault" {
  name                = "teokyllckv"
  resource_group_name = var.ops_keyvault_rg
}

resource "azurerm_key_vault" "dest" {
  name                        = "dest"
  location                    = "eastus"
  resource_group_name         = var.ops_keyvault_rg
  enabled_for_disk_encryption = false
  tenant_id                   = data.azurerm_client_config.current.tenant_id
  purge_protection_enabled    = false

  sku_name = "standard"

  access_policy {
    tenant_id = data.azurerm_client_config.current.tenant_id
    object_id = data.azurerm_client_config.current.object_id

    key_permissions = [
      "Get", "List"
    ]

    secret_permissions = [
      "Get", "List", "Set"
    ]

    storage_permissions = [
      "Get", "List", "Set"
    ]

    certificate_permissions = [
        "Get", "Create", "List", "Import", "Delete"
    ]
  }
}

data "azurerm_key_vault_certificate" "ops_certificate" {
  for_each     = toset(var.system_ops_certificate_names)
  name         = each.key
  key_vault_id = data.azurerm_key_vault.source_vault.id
}

data "azurerm_key_vault_secret" "ops_certificate" {
  for_each     = toset(var.system_ops_certificate_names)
  name         = each.key
  key_vault_id = data.azurerm_key_vault.source_vault.id
}

resource "azurerm_key_vault_certificate" "app_certificate" {
  for_each     = data.azurerm_key_vault_certificate.ops_certificate
  name         = each.value.name
  tags         = each.value.tags
  key_vault_id = azurerm_key_vault.dest.id
  certificate {
    contents = data.azurerm_key_vault_secret.ops_certificate[each.key].value
  }

  dynamic "certificate_policy" {
    for_each = each.value.certificate_policy
    content {
      dynamic "issuer_parameters" {
        for_each = certificate_policy.value.issuer_parameters
        content {
          name = issuer_parameters.value["name"]
        }
      }

      dynamic "key_properties" {
        for_each = certificate_policy.value.key_properties
        content {
          exportable = key_properties.value["exportable"]
          key_size   = key_properties.value["key_size"]
          key_type   = key_properties.value["key_type"]
          reuse_key  = key_properties.value["reuse_key"]
        }
      }

      dynamic "lifetime_action" {
        for_each = certificate_policy.value.lifetime_action
        content {
          dynamic "action" {
            for_each = lifetime_action.value.action
            content {
              action_type = action.value["action_type"]
            }
          }

          dynamic "trigger" {
            for_each = lifetime_action.value.trigger
            content {
              days_before_expiry  = trigger.value["days_before_expiry"]
              lifetime_percentage = trigger.value["lifetime_percentage"]
            }
          }
        }
      }

      dynamic "secret_properties" {
        for_each = certificate_policy.value.secret_properties
        content {
          content_type = secret_properties.value["content_type"]
        }
      }

      dynamic "x509_certificate_properties" {
        for_each = certificate_policy.value.x509_certificate_properties
        content {
          extended_key_usage = x509_certificate_properties.value["extended_key_usage"]
          key_usage          = x509_certificate_properties.value["key_usage"]
          subject            = x509_certificate_properties.value["subject"]
          validity_in_months = x509_certificate_properties.value["validity_in_months"]

          dynamic "subject_alternative_names" {
            for_each = x509_certificate_properties.value.subject_alternative_names
            content {
              dns_names = subject_alternative_names.value["dns_names"]
              emails    = subject_alternative_names.value["emails"]
              upns      = subject_alternative_names.value["upns"]
            }
          }
        }
      }
    }
  }
}
