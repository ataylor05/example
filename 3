data "azurerm_key_vault_secret" "storage_region_secret" {
  for_each     = toset(var.system_storage_keyvault_enabled ? data.azurerm_key_vault_secrets.storage_region_secrets[0].names : [])
  name         = each.key
  key_vault_id = data.azurerm_key_vault.keyvault_storage_region[0].id
}

resource "azurerm_key_vault_secret" "app_secret_region" {
  for_each = {
    for key, value in data.azurerm_key_vault_secret.storage_region_secret :
    key => value if var.system_storage_keyvault_enabled && (trimprefix(value.name, local.secret_name_prefix) != value.name) # starts with secret_name_prefix
  }

  name         = trimsuffix(each.value.name, "--${var.region}") # Trim region suffix from datacenter secrets
  value        = each.value.value
  content_type = each.value.content_type
  tags         = each.value.tags

  key_vault_id = azurerm_key_vault.keyvault_app.id
}
